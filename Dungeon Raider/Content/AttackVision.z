class AttackVision : ZilchComponent
{
    [Property]
    var IsMelee : Boolean = true;
    [Property]
    var DetectionRadius : Real = 1.0;
    [Property]
    var Damage : Real = 1.0;
    
    [Property]
    var Projectile : Archetype = null;
    [Property]
    var CreateDistance : Real = 1.0;
    [Property]
    var Speed : Real;
    
    var Player : Cog = null;
    var DistAway : Real;
    
    [Property] 
    var Cooldown : Real;
    
    var TimeSinceLast : Real = 0.0;
    
    function Initialize(init : CogInitializer)
    {
        this.Player = this.Space.FindObjectByName("Player");
        
        Zero.Connect(this.Space, Events.LogicUpdate, this.OnLogicUpdate);
    }
    
    function OnLogicUpdate(event : UpdateEvent)
    {
        this.GetDist();
        
        if (this.TimeSinceLast > 0.0)
        {
            this.TimeSinceLast -= event.Dt;
        }
        
        if (this.TimeSinceLast <= 0 && this.DistAway <= this.DetectionRadius)
        {
            if (this.IsMelee)
            {
                this.MeleeAttack();
            }
            else
            {
                this.RangedAttack();
            }
            
            this.Owner.EnemyAnimation.IsAttacking = true;
            this.TimeSinceLast = this.Cooldown;
        }
    }
    
    function MeleeAttack()
    {
        if (this.Player != null)
        {
            this.Player.PlayerHealth.CurrentHealth -= this.Damage * this.Player.PlayerHealth.Shield;
        }
    }
    
    function RangedAttack()
    {
        var playerPos = Real3(0, 0, 0);
        if (this.Player != null)
        {
            playerPos = this.Player.Transform.Translation;
        }
        
        var myPos = this.Owner.Transform.Translation;
        var direction = Math.Normalize(playerPos - myPos);
        
        var position = this.Owner.Transform.WorldTranslation + (direction * this.CreateDistance);
        
        var projectile : Cog = null;
        
        projectile = this.Space.CreateAtPosition(this.Projectile, position);
        
        if (projectile != null)
        {
            projectile.RigidBody.Velocity = direction * this.Speed;
            projectile.CollisionDamage.Damage = this.Damage;
        }
        
        this.TimeSinceLast = this.Cooldown;
    }
    
    function GetDist()
    {
        if (this.Player != null)
        {
            //Square root((x2-x1)^2 + (y2-y1)^2) = Resultant distance from the owner to the player
            this.DistAway = Math.Sqrt((Math.Pow((this.Player.Transform.Translation.X - this.Owner.Transform.Translation.X), 2)) 
            + (Math.Pow((this.Player.Transform.Translation.Y - this.Owner.Transform.Translation.Y), 2)));
        }
    }
}
